//----------------------------------------------------
// general configuration that applies to all modules
//----------------------------------------------------
description = 'Mosaic Web Platform'

//-----------------------------------------------
// Java integration
//-----------------------------------------------
apply plugin: 'java'
ext {
    // use jdk 1.7
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

//-----------------------------------------------
// IDEA integration
//-----------------------------------------------
apply plugin: 'idea'    
idea {
    module {
        excludeDirs += file('idea')
        excludeDirs += file('run/mosaic/work')
    }
}

//-----------------------------------------------
// configuration for all modules
//-----------------------------------------------
allprojects {

    //-----------------------------------------------
    // general configuration
    //-----------------------------------------------
    group = 'org.mosaic'
    version = '0.1.0.DEV'
    ext {
        vendor = 'Arik Kfir'

        // dependency management
        dependencies = {}
        apply from: "$rootProject.projectDir/dependencies.gradle", to: dependencies
    }
}

//-----------------------------------------------
// configure child projects
//-----------------------------------------------
subprojects {
    //-----------------------------------------------
    // setup compilation
    //-----------------------------------------------
    apply plugin: 'java'
    apply plugin: 'osgi'

    ext {
        // use jdk 1.7
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }
    
    // compiler configuration
    compileJava {
        options.compilerArgs = [ '-Xlint:unchecked' ]
        options.debug( debugLevel: 'lines,vars,source' )
        options.deprecation = true
        options.encoding = 'UTF-8'
        options.warnings = true
    }

    //-----------------------------------------------
    // IDEA integration
    //-----------------------------------------------
    apply plugin: 'idea'    
    idea {
        pathVariables("GRADLE_USER_HOME": gradle.gradleUserHomeDir)
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    //-----------------------------------------------
    // set-up dependency resolution
    //-----------------------------------------------
    apply plugin: 'maven'

    // we will use the standard maven repository
    repositories {
        mavenCentral()
    }

    // disable dependency transitivity - maven repos have lots of junk
    configurations.all {
        transitive = false
    }

}

